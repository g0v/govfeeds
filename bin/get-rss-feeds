#!/usr/bin/env lsc
# -*- livescript -*-

require! <[ cheerio fs request ]>

save-as = (file, feeds) ->
  fs.write-file file, JSON.stringify feeds, \utf8
  process.stdout.write "#file produced.\n"

grok = (url, file, cb) ->
  _err, _res, page <- request url
  base = (url / '/')[0 2] * '//'
  links = []
  take = ->
    it = "#base#it" if it is /^\//;
    links.push it
  $ = cheerio.load page
  cb $, take
  save-as "data/#file.json", links

get-feeds-govtw = ->
  $, take <- grok \http://rss.www.gov.tw \gov-tw-list
  $ ".list_item a:first-child" .each -> take @attr \href

get-feeds-npllygovtw = ->
  $, take <- grok \http://npl.ly.gov.tw/do/forward?dest=www.rss \np-ly-gov-tw-list
  $ "a[href*=Rss]" .each -> take @attr \href

get-feeds-wwwlygovtw = ->
  $, take <- grok \http://www.ly.gov.tw/01_lyinfo/0111_rss/rss01.js \www-ly-gov-tw-list
  $ ".rss_bg a" .each -> take @attr \href

get-feeds-govtw!
get-feeds-npllygovtw!
get-feeds-wwwlygovtw!
