#!/usr/bin/env lsc
# -*- livescript -*-

require! <[ cheerio fs request async ]>
Url = require \url

save-as = (file, feeds) ->
  fs.write-file file, JSON.stringify feeds, \utf8, 4
  process.stdout.write "#file produced.\n"

rss-channel-title = (url, cb) ->
  _err, _res, page <- request url
  $ = cheerio.load page
  cb $("channel > title").text!

get-titles = (links, cb) ->
  links2 = []
  take2 = -> links2.push it
  add-title = (feed-url, cb2) ->
    rss-channel-title feed-url, ->
      take2 { url: feed-url, title: it }
      cb2!

  async.for-each links, add-title, (err, results) ->
    if err                 
       process.stderr.write(err + "\n")
    else
       cb(links2)

grok = (url, file, cb) ->
  _err, _res, page <- request url
  links = []
  take = -> links.push Url.resolve(url, it)
  $ = cheerio.load page
  cb $, take
  get-titles links, (links2) -> save-as "data/#file.json", links2

get-feeds-govtw = ->
  $, take <- grok \http://rss.www.gov.tw \gov-tw-list
  $ ".list_item a:first-child" .each -> take @attr \href

get-feeds-npllygovtw = ->
  $, take <- grok \http://npl.ly.gov.tw/do/forward?dest=www.rss \np-ly-gov-tw-list
  $ "a[href*=Rss]" .each -> take @attr \href

get-feeds-wwwlygovtw = ->
  $, take <- grok \http://www.ly.gov.tw/01_lyinfo/0111_rss/rss01.jsp \www-ly-gov-tw-list
  $ ".rss_bg a" .each -> take @attr \href

## bug5 page. Nothing works. Manually get rss links.
get-feeds-wwwstatgovtw = ->
  # $, take <- grok { url: "http://www.stat.gov.tw/ct.asp?xItem=24186&ctNode=5083&mp=4", encoding:\ascii } \www-stat-gov-tw-list
  # $("\#ContentField a").each -> take @attr \href
  links =
    * "http://www.stat.gov.tw/rss/StateNews.xml"
    * "http://www.stat.gov.tw/rss/StateStat.xml"
  get-titles links, (links2) -> save-as "data/www-stat-gov-tw-list.json", links2

get-feeds-wwwcwbgovtw = ->
  $, take <- grok \http://www.cwb.gov.tw/V7/service/eservice/rss.htm \www-cwb-gov-tw-list
  $ ".BoxTableee a" .each -> take that.0 if @attr \href .match /^\/rss\/(forecast|Data)\/.+$/

async.parallel [
  * get-feeds-govtw
  * get-feeds-npllygovtw
  * get-feeds-wwwlygovtw
  * get-feeds-wwwstatgovtw
  * get-feeds-wwwcwbgovtw
], -> process.stdout.write \done
