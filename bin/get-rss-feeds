#!/usr/bin/env lsc
# -*- livescript -*-

require! <[ cheerio fs request async ]>
Url = require \url

save-as = (file, feeds) ->
  fs.write-file file, JSON.stringify feeds, \utf8, 4
  process.stdout.write "#file produced.\n"

rss-channel-title = (url, cb) ->
  _err, _res, page <- request url
  $ = cheerio.load page
  cb $("channel > title").text!

get-titles = (links, cb) ->
  links2 = []
  take2 = -> links2.push it
  add-title = (feed-url, cb2) ->
    rss-channel-title feed-url, ->
      take2 { url: feed-url, title: it }
      cb2!

  async.for-each links, add-title, (err, results) ->
    if err                 
       process.stderr.write(err + "\n")
    else
       cb(links2)

grok = (url, cb) ->
  file = (url / "/")[2] .replace /\./g, "-"
  _err, _res, page <- request url
  links = []
  take = -> links.push Url.resolve(url, it)
  $ = cheerio.load page
  cb $, take
  get-titles links, (links2) -> save-as "data/#file.json", links2

get-feeds-govtw = ->
  $, take <- grok \http://rss.www.gov.tw
  $ ".list_item a:first-child" .each -> take @attr \href

get-feeds-npllygovtw = ->
  $, take <- grok \http://npl.ly.gov.tw/do/forward?dest=www.rss
  $ "a[href*=Rss]" .each -> take @attr \href

get-feeds-wwwlygovtw = ->
  $, take <- grok \http://www.ly.gov.tw/01_lyinfo/0111_rss/rss01.jsp
  $ ".rss_bg a" .each -> take @attr \href

## bug5 page. Nothing works. Manually get rss links.
get-feeds-wwwstatgovtw = ->
  # $, take <- grok { url: "http://www.stat.gov.tw/ct.asp?xItem=24186&ctNode=5083&mp=4", encoding:\ascii } \www-stat-gov-tw
  # $("\#ContentField a").each -> take @attr \href
  links =
    * "http://www.stat.gov.tw/rss/StateNews.xml"
    * "http://www.stat.gov.tw/rss/StateStat.xml"
  get-titles links, (links2) -> save-as "data/www-stat-gov-tw.json", links2

get-feeds-wwwcwbgovtw = ->
  $, take <- grok \http://www.cwb.gov.tw/V7/service/eservice/rss.htm
  $ ".BoxTableee a" .each -> take that.0 if @attr \href .match /^\/rss\/(forecast|Data)\/.+$/

get-feeds-wwwmofagovtw = ->
  $, take <- grok \http://www.mofa.gov.tw/RSS/Index/?opno=a280c610-3f75-471f-a3c1-e0883e7122f7
  $ ".degree a" .each -> take @attr \href

get-feeds-wwweygovtw = ->
  $, take <- grok \http://www.ey.gov.tw/cp.aspx?n=4E183D9D41028977
  $ "\#ContentPlaceHolder1_dlindex a" .each -> take that.0 if @attr \href .match /^.*Rss_News\.aspx.+$/

get-feeds-wwwmocgovtw = ->
  $, take <- grok \http://www.moc.gov.tw/rss.do
  $ ".t13bk a" .each (i) -> take @attr \href if i > 1

get-feeds-nmhgovtw = ->
  $, take <- grok \http://www.nmh.gov.tw/zh-tw/Index/RSS.aspx?unkey=RSS
  $ '#oDV_Friendly_Content a[href]' .each -> take @attr \href

get-feeds =
  * get-feeds-govtw
  * get-feeds-npllygovtw
  * get-feeds-wwwlygovtw
  * get-feeds-wwwstatgovtw
  * get-feeds-wwwcwbgovtw
  * get-feeds-wwwmofagovtw
  * get-feeds-wwweygovtw
  * get-feeds-wwwmocgovtw
  * get-feeds-nmhgovtw

# get-feeds = [ get-feeds[ * - 1 ] ]

async.parallel get-feeds, -> process.stdout.write \done
