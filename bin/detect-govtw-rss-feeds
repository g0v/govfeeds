#!/usr/bin/env perl
use v5.14;
use strict;
use warnings;

use URI;
use LWP::UserAgent::Cached;
use Web::Query;
use XML::FeedPP;

my $url = $ARGV[0] or die;

my $ua = $Web::Query::UserAgent = LWP::UserAgent::Cached->new(cache_dir => "/tmp/lwp-cache");

my $base_url = URI->new($url);

my %seen= ();
my $page = wq($url);
my $links = $page->find("a")->filter(
    sub {
        return 0 unless $_->attr("href") && $_->attr("href") =~ m{\A(http:// | /)}x;
        my $u = URI->new_abs( $_->attr("href"), $base_url );
        return 0 if $u->host =~ /(facebook|plurk|twitter)\.com\z/;
        return 0 if $u->path eq "/" || $u->path eq "";

        $_->attr("href", "$u");
        return $seen{$u} ? 0 : ($seen{$u} = 1);
    }
)->filter(
    sub {
        my $response = $ua->get($_->attr("href"));
        return 0 unless $response->is_success;

        my $ct = $response->header("Content-Type");
        return 0 unless $ct && $ct =~ m{(text|atom|rss|xml)}x;

        my $feed;

        eval {
            $feed = XML::FeedPP->new(
                $response->content,
                ignore_error => 1,
                -type => "string"
            );
            1;
        };

        return ($feed && $feed->isa("XML::FeedPP"));
    }
)->each(sub{ say $_->attr("href");  });
